""" 
RSV-A Sequencing Analysis Pipeline.
Author: Will Hannon 
"""

#### ----------------------- Imports ----------------------- ####

import pandas as pd 
from os.path import join

#### -------------------- Configuration -------------------- ####

configfile: "configuration/pipeline.yml"

#### ----------------------- Targets ----------------------- ####
sample_df = pd.read_csv(config['samples'])

rule all:
    input:
        expand(join(config['qc_dir'], "{sample}", "{sample}.fastp.html"), sample=sample_df['Run'].unique()),
        expand("results/reference/{gene}.fasta", gene=config['genes'])

#### ------------------------ Rules ------------------------ ####

rule clean:
    shell:
        """
        rm -rf logs/
        rm -rf tmp/
        rm -f slurm*.out
        """


rule parse_gene_from_reference:
    output:
        outpath = "results/reference/{gene}.fasta"
    params:
        fasta_url = config['ref'],
        gff_url = config['gff']
    shell:
        """
        python workflow/scripts/parse_gene.py {params.fasta_url} {params.gff_url} {wildcards.gene} {output.outpath}
        """


rule fetch_fastq:
    """ 
    Move the fastq files into the results directory and rename the files. 
    """
    output: R1 = join(config['fastq_dir'], "{sample}", "{sample}_R1.fastq.gz"),
            R2 = join(config['fastq_dir'], "{sample}", "{sample}_R2.fastq.gz")
    params: R1_path = lambda wildcards: sample_df.loc[(sample_df.Run == wildcards.sample)].R1.item(),
            R2_path = lambda wildcards: sample_df.loc[(sample_df.Run == wildcards.sample)].R2.item()
    shell: "cp {params.R1_path} {output.R1} && cp {params.R2_path} {output.R2}"


rule trim_adapters:
    """
    Use fastp to trim the adapters from the reads.

    1. Automatic adaptor trimming
    2. Low-qual base filtering (40% of bases w/ Phred >15) 
    3. Reporting by HTML and JSON.
    """
    input:  
        R1 = join(config['fastq_dir'], "{sample}", "{sample}_R1.fastq.gz"),
        R2 = join(config['fastq_dir'], "{sample}", "{sample}_R2.fastq.gz")
    output:
        R1 = join(config['trim_dir'], "{sample}", "{sample}_R1.fastq.gz"),
        R2 = join(config['trim_dir'], "{sample}", "{sample}_R2.fastq.gz"),
        html = join(config['qc_dir'], "{sample}", "{sample}.fastp.html"),
        json = join(config['qc_dir'], "{sample}", "{sample}.fastp.json")
    conda: 'envs/process-reads.yml'
    shell:
        """ 
        fastp \
            -i {input.R1} \
            -I {input.R2} \
            -o {output.R1} \
            -O {output.R2} \
            --html {output.html} \
            --json {output.json} 
        """
